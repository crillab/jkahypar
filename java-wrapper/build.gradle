/*
 * This is the build.gradle configuration file of the java-wrapper
 * for KaHyPar.
 */

/************************
 * Module Configuration *
 ************************/

apply plugin: "java"
apply plugin: "jacoco"

ext.moduleName = "fr.univartois.cril.jkahypar"

sourceCompatibility = "11"
targetCompatibility = "11"

/****************
 * Dependencies *
 ****************/

repositories {
    mavenCentral()
}

dependencies {
    implementation "net.java.dev.jna:jna:5.12.0"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.1"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.5.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.5.1"
}

/*************************
 * Eclipse Configuration *
 *************************/

eclipse.classpath.file.whenMerged {
    // Modularizing project dependencies.
    entries.findAll{ isModule(it) }.each{ it.entryAttributes["module"] = "true" }
}

/****************************************
 * Compilation Configuration for Jigsaw *
 ****************************************/

afterEvaluate {
    compileJava {
        // Setting the module name.
        inputs.property("moduleName", moduleName)

        // Adding the classpath to the module path.
        doFirst {
            options.compilerArgs = [
                "--module-path", classpath.asPath
            ]
            classpath = files()
        }
    }
}

/**********************
 * Test Configuration *
 **********************/

// Making test resources available during test execution.
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/java/test"
}
processTestResources.dependsOn copyTestResources

// Configuring the use of JUnit 5.
test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = "full"
    }
}

// Configuring the version of JaCoCo to use.
jacoco {
    toolVersion = "0.8.7"
}

// Asking for an XML output for JaCoCo.
jacocoTestReport {
    reports {
        xml.required = true
    }
}

/*************
 * Packaging *
 *************/

// Putting the JAR in the distribution directory.
tasks.withType(Jar) {
    archiveBaseName = "jkahypar"
    destinationDirectory = file("${rootDir}/dist/jars")
}

// Collects the external dependencies of JKaHyPar into the output directory.
task collectExternalDependencies(type: Copy) {
    from configurations.runtimeClasspath.collect { it }
    into "${rootDir}/dist/jars"
}

// Creating a gzipped-tarball with JKaHyPar and its dependencies.
task jkahypar(type: Tar) {
    dependsOn jar
    dependsOn collectExternalDependencies

    from file("${rootDir}/dist/jars"), file("../README.md")
    destinationDirectory = file("${rootDir}/dist")
    archiveBaseName = "jkahypar"
    archiveExtension = "tgz"

    compression = Compression.GZIP
}

// Removes the binary files of JKaHyPar when cleaning.
task removeBinaries(type: Delete) {
    delete "dist"
}
clean.dependsOn removeBinaries

/*************
 * Functions *
 *************/

// Checking whether an Eclipse classpath entry must be put in the module path.
boolean isModule(entry) {
    (entry.kind == "src") || ((entry.kind == "lib") && (entry.entryAttributes["gradle_used_by_scope"] != "test"))
}
